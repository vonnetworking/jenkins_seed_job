buildscript {
    repositories {
        maven { url "http://nimble-bin.fhlmc.com/artifactory/libs-snapshot" }
        maven { url "http://nimble-bin.fhlmc.com/artifactory/libs-release" }
    }

    dependencies {
        classpath 'com.fhlmc.nimble:ndm-gradle-plugin:+'

    }
}

// More information can be found here
// https://github.com/sheehan/job-dsl-gradle-example/blob/master/gradle.properties

apply plugin: 'ndm'
apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs '**/*.groovy'
        }
    }
}

// https://github.com/sheehan/job-dsl-gradle-example/blob/master/gradle.properties
ext {
    jobDslVersion = '1.68'
    jenkinsVersion = '2.73'
}

configurations {
    testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.8'
    compile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.8'
    testCompile("org.jenkins-ci.main:jenkins-war:${jenkinsVersion}") {
        exclude group: 'org.jenkins-ci.ui', module: 'bootstrap' // https://github.com/sheehan/job-dsl-gradle-example/issues/87
    }

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.9@jar'
    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:5.12@jar'

    // plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.31.4'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.19.0'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:5.12'
    testPlugins 'org.jenkins-ci.plugins:credentials:2.1.10'
    testPlugins 'org.jenkins-ci.plugins:gitlab-plugin:1.5.2'

    // plugins used for auto-generated DSL. See example 9.
    testPlugins 'org.jenkins-ci.plugins:cvs:2.13'
    testPlugins ('org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:2.7') {
        exclude group: 'com.cloudbees', module: 'diff4j'
        exclude group: 'org.jvnet', module: 'localizer'
    }

    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.0'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {

    finalizedBy jacocoTestReport
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

tasks.getByName('test').inputs.files(sourceSets.jobs.groovy.srcDirs)

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}
